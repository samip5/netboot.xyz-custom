#!ipxe

isset ${dhcp-server} && set ipparam ip=dhcp || set ipparam ip=${ip}::${gateway}:${netmask}:::none nameserver=${dns}
set ipparam BOOTIF=${netX/mac} ${ipparam}

goto ${menu} ||


menu Utilities - Image Sig Checks: [${img_sigs_enabled}]
item --gap Utilities:
item avg ${space} AVG Rescue CD
item clonezilla ${space} Clonezilla
item dban ${space} DBAN
item gparted ${space} GParted
item grml ${space} Grml
item hdt ${space} Hardware Detection Tool
item memtest-420 ${space} Memtest86+ 4.20
item memtest-501 ${space} Memtest86+ 5.01
item partition-wizard ${space} Partition Wizard
item pogostick ${space} Pogostick - Offline Windows Password and Registry Editor
item supergrub ${space} Super Grub2 Disk
item ubcd ${space} Ultimate Boot CD (UBCD)
item --gap netboot.xyz tools:
item nbxyz-custom ${space} Set Github User [user: ${github_user}]
item testdistro ${space} Test Distribution ISO
item testpr ${space} Test netboot.xyz branch
choose --default ${menu} menu || goto utils_exit
echo ${cls}
goto ${menu} ||
chain ${menu}.ipxe || goto utils_exit
goto utils_exit

:ubcd
set util_path 185.218.193.132/ubcd538.iso
set util_file ubcd538.iso
goto boot_memdisk

:boot_memdisk
imgfree
kernel ${memdisk} iso raw
initrd http://${util_path}
echo
echo MD5sums:
md5sum memdisk ${util_file}
iseq ${img_sigs_enabled} true && goto verify_sigs || goto skip_sigs
:verify_sigs
echo
echo Checking signatures...
imgverify memdisk ${sigs}memdisk.sig || goto error
imgverify ${util_file} ${sigs}${menu}/${util_file}.sig || goto error
echo Signatures verified!
echo
:skip_sigs
boot
goto utils_exit


:utils_exit
clear menu
exit 0
